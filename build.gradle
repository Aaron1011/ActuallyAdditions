buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'idea'

version = "1.12.1-r121"
group = "de.ellpeck.actuallyadditions"
archivesBaseName = "ActuallyAdditions"

if(hasProperty('buildnumber')){
    version = "${version}-${this.properties['buildnumber']}"
}

minecraft {
    version = "1.12.1-14.22.1.2481"
    runDir = "run"
    mappings = "snapshot_20170821"
    replaceIn "ModUtil.java"
    replace "@VERSION@", project.version.toString()
}

repositories {
    maven {
        url "http://dvs1.progwml6.com/files/maven"
    }
    maven {
        url "http://maven.epoxide.xyz"
    }
    maven {
        url "http://maven.amadornes.com/"
    }
	maven {
		url "http://tehnut.info/maven"
	}
	maven {
		url "http://maven.ic2.player.to"
	}
	maven {
        url "https://dl.bintray.com/cyclopsmc/dev/"
    }
}

dependencies {
	deobfCompile "mezz.jei:jei_1.12.1:+"
	deobfCompile "mcp.mobius.waila:Hwyla:+"
    compile "net.darkhax.tesla:Tesla-1.12:+"
    deobfCompile "org.cyclops.commoncapabilities:CommonCapabilities:1.12-1.3.3+"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {

}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    from sourceSets.main.java
    classifier = 'dev'
}

task apiJar(type: Jar) {
    from sourceSets.main.output
    from sourceSets.main.java
    classifier = 'api'
    include 'de/ellpeck/actuallyadditions/api/**'
}

javadoc {
    include 'de/ellpeck/actuallyadditions/api/**'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from 'build/docs/javadoc'
    classifier 'javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.java
    classifier = 'sources'
}

apply plugin: 'maven-publish'

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
            artifact apiJar
            artifact javadocJar
            artifact sourcesJar
            artifact deobfJar
        }
    }
    repositories {
        maven {
            url "file:///srv/nginx/maven"
        }
    }
}